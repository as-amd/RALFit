***************************
* SET UP THE INITIAL DATA *
***************************

NAME          MISRA1A 

*   Problem :
*   *********

*   NIST Data fitting problem MISRA1A given as an inconsistent set of 
*   nonlinear equations

*   Fit: y = b1*(1-exp[-b2*x]) + e

*   Source:  Problem from the NIST nonlinear regression test set
*     http://www.itl.nist.gov/div898/strd/nls/nls_main.shtml

*   Reference: Misra, D., NIST (1978).  
*     Dental Research Monomolecular Adsorption Study.

*   SIF input: Nick Gould and Tyrone Rees, Oct 2015
*   Modification as a set of nonlinear equations: Nick Gould, Oct 2015.

*   classification NOR2-MN-2-14

*   Number of data values

 IE M                   14

*   Number of variables

 IE N                   2

*   Useful parameters

 IE 1                   1

*  Data values

 RE X1                   77.6E0      
 RE X2                  114.9E0      
 RE X3                  141.1E0      
 RE X4                  190.8E0      
 RE X5                  239.9E0      
 RE X6                  289.0E0      
 RE X7                  332.8E0      
 RE X8                  378.4E0      
 RE X9                  434.8E0      
 RE X10                 477.3E0      
 RE X11                 536.8E0      
 RE X12                 593.1E0      
 RE X13                 689.1E0      
 RE X14                 760.0E0      

 RE Y1                  10.07E0
 RE Y2                  14.73E0
 RE Y3                  17.94E0
 RE Y4                  23.93E0
 RE Y5                  29.61E0
 RE Y6                  35.18E0
 RE Y7                  40.02E0
 RE Y8                  44.82E0
 RE Y9                  50.76E0
 RE Y10                 55.05E0
 RE Y11                 61.01E0
 RE Y12                 66.40E0
 RE Y13                 75.47E0
 RE Y14                 81.78E0

VARIABLES

 DO I         1                        N
 X  B(I)
 ND

GROUPS

 DO I         1                        M
 XE F(I)      B1        1.0
 ND

CONSTANTS

 DO I         1                        M
 Z  MISRA1A   F(I)                     Y(I)
 ND        

BOUNDS

 FR MISRA1A   'DEFAULT'

START POINT

    START1    B1        500.0
    START1    B2        0.0001

*  Alternative start point

    START2    B1        250.0    
    START2    B2        0.0005 


ELEMENT TYPE

 EV E2        V1
 EV E2        V2
 EP E2        X

ELEMENT USES

 DO I         1                        M
 XT E(I)      E2
 ZV E(I)      V1                       B1
 ZV E(I)      V2                       B2
 ZP E(I)      X                        X(I)
 ND

GROUP USES

 DO I         1                        M
 XE F(I)      E(I)      -1.0
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO MISRA1A             0.0

*   Solution

*LO SOLTN               

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      MISRA1A 

TEMPORARIES

 R  E
 M  EXP

INDIVIDUALS

 T  E2
 A  E                   EXP( - V2 * X )
 F                      V1 * E
 G  V1                  E
 G  V2                  - V1 * X * E
 H  V1        V2        - X * E
 H  V2        V2        V1 * X * X * E

ENDATA

